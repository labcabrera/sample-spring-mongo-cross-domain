buildscript {
	ext {
		rsqlQuerydslVersion     = '2.0.0.RELEASE'
		springfoxSwaggerVersion = '2.9.2'
		restQueryEngineVersion  = '0.7.1'
		restAssuredVersion      = '3.2.0'
	}
}

plugins {
	id 'org.springframework.boot'           version '2.1.0.RELEASE' apply false
	id 'com.palantir.jacoco-coverage'       version '0.4.0'         apply false
}

subprojects {

	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.palantir.jacoco-coverage'
	
	group = 'org.lab.samples.mongo'
	version = '0.6.0-SNAPSHOT'
	sourceCompatibility = 1.8
	
	repositories {
		mavenCentral()
	}

}

project(':sample-shared-model') {
	dependencies {
		implementation("com.github.vineey:rsql-querydsl-all:${rsqlQuerydslVersion}")
		implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
		implementation('org.springframework.boot:spring-boot-starter-validation')
		compileOnly('org.projectlombok:lombok')
	}
	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
		}
	}
}

project(':sample-api-model') {
	dependencies {
		implementation(project(':sample-shared-model'))
		implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
		implementation('org.springframework.boot:spring-boot-starter-validation')
		implementation('org.springframework.boot:spring-boot-starter-json')
		implementation("com.github.vineey:rsql-querydsl-all:${rsqlQuerydslVersion}")
		implementation('org.projectlombok:lombok')
	}
	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
		}
	}
}

project('sample-api') {
	apply plugin: 'org.springframework.boot'
	dependencies {
		implementation(project(':sample-shared-model'))
		implementation(project(':sample-api-model'))
		implementation('org.springframework.boot:spring-boot-starter-actuator')
		implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
		implementation('org.springframework.boot:spring-boot-starter-hateoas')
		implementation('org.springframework.boot:spring-boot-starter-web')
		implementation('com.querydsl:querydsl-core')
		implementation("com.github.vineey:rsql-querydsl-all:${rsqlQuerydslVersion}")
		implementation("io.springfox:springfox-swagger2:${springfoxSwaggerVersion}")
		implementation("io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}")
		implementation("com.github.rutledgepaulv:rest-query-engine:${restQueryEngineVersion}")
		compileOnly('org.projectlombok:lombok')
		testImplementation('org.springframework.boot:spring-boot-starter-test')
		testImplementation("io.rest-assured:rest-assured:${restAssuredVersion}")
	}
}
